apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.github.dcendents.android-maven'

ext {
    GROUP = 'com.algolia.instantsearch-android'
    BASENAME = 'voice'
    CODENAME = "$GROUP:$BASENAME"
    NAME = 'InstantSearch Android Voice'
    DESC = "Easily add Voice search to your Android apps"
    LABELS = ["voice search", "voice input", "permissions", "voice UI"]

    GITHUB = "algolia/instantsearch-android-voice"
    WEBSITE = "https://github.com/$GITHUB"
    REPO = WEBSITE + ".git"
    LICENSE = 'MIT'
    LICENSE_URL = "http://www.opensource.org/licenses/mit-license.php"

    VERSION = '1.0.1'
    VERSION_DESC = "$NAME - v$VERSION"
}

android {
    compileSdkVersion 27

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

kotlin {
    experimental {
        coroutines 'enable'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$VERSION_KOTLIN"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:0.23.4"

    // region UI
    implementation "com.android.support:appcompat-v7:$VERSION_SUPPORT"
    implementation "com.android.support:design:$VERSION_SUPPORT"
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    // endregion

    // region Tests
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    // endregion

}

// region Deployment
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.github.dcendents.android-maven'

task sourcesJar(type: Jar) {
    dependsOn "assembleRelease"
    classifier "sources"
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator)) // dependencies
    options.links("http://docs.oracle.com/javase/8/docs/api")
    options.linksOffline("http://d.android.com/reference", "$System.env.ANDROID_HOME/docs/reference")
    options.memberLevel = JavadocMemberLevel.PROTECTED
}

afterEvaluate {
    // we need this as javadoc task's body is called before android.libraryVariants is filled. http://stackoverflow.com/a/34572606/3109189
    javadoc.classpath += files(android.libraryVariants.collect { variant ->
        variant.getJavaCompiler().classpath.files
    })
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier "javadoc"
}

// add sources jar tasks as artifacts
artifacts {
    archives sourcesJar
    archives javadocJar
    archives file: new File("${project.buildDir}/outputs/aar/${project.name}-release.aar"),
            name: "${project.name}-release.aar",
            type: "aar"
}

apply from: "https://raw.githubusercontent.com/algolia/instantsearch-mobile-tools/7b9e6e1cc28f12497b88b6868ab99d5884f7c86b/gradle/bintrayv.gradle"
// endregion
